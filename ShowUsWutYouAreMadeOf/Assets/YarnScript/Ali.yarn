title: Ali
---
//maybe have the first interact subject someone also an artist, and you discuss about sketching queer people!
<<camera AliChat>>
<<if visited_count("Ali") == 0>>
    <<set $NumberOfEncounters +=1>>
    <<set $SketchIndex to 0>> //for which body part
    <<set $MaxStrokes to 10>> //how many strokes before call it a day
    <<set $StrokeCount to 0>> //how many strokes have already made
    <<set $SpecialDialogue to false>> //in respond to wearing their gift
    <<set $SpecialDialogueSpoken to false>> 
    
    <<set $AliSmallTalk to 0>>

    <<pose Ali Introchat>>
    <<wait .5>>
    Ali: Hey!
    Ali: Haven’t seen you around before. I’m Ali. 
    <<pose Ali Chat .5>>
    Ali: Saw you sketching earlier. Are you an artist?
    <<pose Ali CExplain .5>>    
    Ali: I was at this art market a few weeks ago. An artist was doing some live portrait drawing.
    Ali: It's intriguing to see yourself on a flat piece of paper, not quite like photos.
    <<pose Ali CThinklook .5>>
    Ali: Afterall, I was being carefully watched for a period of time.
    <<pose Ali CThink .5>>
    Ali: I thought, so that's how they see me.
    Ali: ...
    <<pose Ali CSurprised .4>>
    Ali: You would like to sketch me? 
    <<pose Ali CHandsonchest .4>>
    Ali: Wow, what a luck to be asked!
    <<pose Ali CHair .5>>
    Ali: Is it my hair that has caught your eyes?
    <<pose Ali Chat .5>>
    Ali: That would be my absolute honour.
    <<sketch true>>
    <<camera AliSketch>>
    <<pose Ali Sketch .5>>
    <<wait .5>>
    Ali: Once talked to a client who’s a life model. You know, posing for life drawings? It’s quite fascinating. 
    <<startsketch>>

<<else>>
    <<if $SpecialDialogue == true>>
        <<jump AliSpecial>>
    <<elseif $AliSmallTalk == 0>>
        <<pose Ali Chat>>
        Ali: Hey! Thanks for the drawing earlier.
        <<pose Ali Explain .5>>
        Ali: We were discussing about the benefit of colorful hair.
        Ali: Once met this person who used her blue hair as a strategy, so people remember her in networking events.
    <<elseif $AliSmallTalk == 1>>
        <<pose Ali Explain>>
        Ali: My horoscope? I'm a Sagittarius, does that mean I don't get along with a certain group of people?
    <<elseif $AliSmallTalk == 2>>
        <<pose Ali Introchat>>
        Ali: I showed the picture of your drawing to my friends, they were very impressed too. 
        <<pose Ali Chat .5>>
        Ali: It's a way to break the ice, getting to know people a bit.
        <<pose Ali Explain .5>>
        Ali: Even flirt a bit, maybe.
        Ali: Coming from one of their suggestions.
        <<silence Ali>>
        //<<leave Ali>>
        //<<enter Toni>>
    <<endif>>

    <<camera Player>>
    <<pose Ali Sit>>
    <<set $AliSmallTalk +=1>>
<<endif>>

===
title: AliSpecial
---
<<if visited_count("AliSpecial") == 0>>
    <<pose Ali CThinklook>>
    Ali: Hey, that hairstyle looks good on you!
    Ali: It's nice to give a change in style sometimes.
    <<pose Ali CExplain .5>>
    Ali: It may not work everytime, but I think this hair suits you!
<<elseif visited_count("AliSpecial") == 1>>
    Ali: It always put a smile on my face when I see people embracing their queerness with crazy hair.
    <<pose Ali CThinklook .5>>
    Ali: I think lilac might look good on you.
    <<pose Ali CExplain .5>>
    Ali: We got a hairdye specialist who comes here often. Will give you a shout if I spot them tonight!
<<else>>
    <<set $SpecialDialogueSpoken to true>>
    <<set $SpecialDialogue to false>>
    <<jump Ali>>
<<endif>>

<<camera Player>>
<<pose Ali Sit>>
===
title: AliSketch
---

<<if $StrokeCount < $MaxStrokes>>
    <<set $StrokeCount +=1>>

    <<if $SketchIndex == 0>>
    <<pose Ali Hair .25>>
    <<jump Ali_Hair>>
    <<elseif $SketchIndex ==1>>
    <<pose Ali Body .25>>
    <<jump Ali_Body>>
    <<elseif $SketchIndex ==2>>
    <<pose Ali Eye .5>>
    <<jump Ali_Eye>>
    <<elseif $SketchIndex ==3>>
    <<pose Ali Top .5>>
    <<jump Ali_Top>>
    <<elseif $SketchIndex ==4>>
    <<pose Ali Tattoo .5>>
    <<jump Ali_Tattoo>>
    <<endif>>
<<else>>
    <<pose Ali Laststroke .5>>
    <<jump AliLastStroke>>
<<endif>>
===
title: AliLastStroke
---
    Ali: Seems like my friends are arriving. I’m going to say hi. 
    <<pose Ali Sketch>>
    Ali: Come visit my salon some day, so we can continue this conversation.
    <<lastdraw>>
    <<wait .5>>
    <<set $StrokeCount to 0>>
    <<sketchfin>>
===
title: AliSketchFin
---
<<sketch false>>
<<camera Player>>
<<set $AliSketched to true>>
<<pose Ali Thinklook>>
Ali: Oh, I love your choice of colour.
Ali: It's fascinating to see how you see me in this drawing. It make me think of the conversation we had.
<<pose Ali Explain .5>>
Ali: Feel free to join us anytime tonight! If not, hope I’ll see you again soon. 
<<pose Ali Chat .5>>
Ali: Here, or at my salon! 
<<gift Ali>>
<<pose Ali Sit>>
<<camera Player>>
===
title: Ali_Hair
---
<<wait .5>>
<<if visited_count("Ali_Hair") == 0>>
    Ali: My hair? I'm actually trying something new. It got a bit long so I just shaved off the sides.
<<elseif visited_count("Ali_Hair") == 1>>
    Ali: What's in your mind when you’re drawing your subject?
    Ali: I work in a salon. Usually I talk to my client when I'm giving haircuts. 
    Ali: Getting to know who they are, why they want that hairstyle…
    Ali: Then I have a better understanding of what represents them the best, and how they want to be seen.
<<elseif visited_count("Ali_Hair") == 2>>
    Ali: The first time I shaved my head I felt so liberated. 
    Ali: I always wanted to do that but never had the need, or reason to. 
    Ali: Until we had this occassion, where someone dared me to shave it all off.
    Ali: I did. And I looked at myself in the mirror. It was someone new, someone different. 
    Ali: Someone hiding inside of me that wanted to be seen for a long time.
<<elseif visited_count("Ali_Hair") == 3>>
    Ali: There’s a reason why hair is so important for many queer folks.
    Ali: It’s very fulfilling to be in a hair space, where queer visibility is fostered to flourish.
    Ali: It's always delightful to see people leave with a look that they relate themselves more than the one they came in with. 
<<endif>>

<<if $StrokeCount >= $MaxStrokes>>
    <<jump AliSketch>>
<<endif>>
<<pose Ali Sketch .5>>
===
title: Ali_Body
---
<<wait .5>>
<<if visited_count("Ali_Body") == 0>>
    Ali: People think I must have been one of those cool kids in school. 
    Ali: But back in the days I only wanted to fit in.
<<elseif visited_count("Ali_Body") == 1>>
    Ali: I don't see myself fitting in the binary anymore. 
    Ali: But I'm comfortable presenting the feminine side of me. So currently I define myself with she/they the most.
    <<pronoun>>
    Ali: It shouldn't be black or white anyways.
<<elseif visited_count("Ali_Body") == 2>>
    Ali: I felt brave for looking different. 
    Ali: Gender and style expressions. For me it's all a form of rebellion.
    Ali: But not only that, I am acting myself this time. For once I could abandon the fear of not fitting in. I was comfortable in my skin.
    Ali: People started to recognizing me as queer. And especially queer people, too.
    Ali: I felt so visible. I never looked back.
<<elseif visited_count("Ali_Body") == 3>>
    Ali: There's no rulebook of what queerness look like.
    Ali: By defining a queer look, you’re putting yourself into box again!
    Ali: Whatever hairstyle, long or short, common or offbeat, if you wear that, then it is a queer style.
<<endif>>

<<if $StrokeCount >= $MaxStrokes>>
    <<jump AliSketch>>
<<endif>>
<<pose Ali Sketch .5>>
===
title: Ali_Eye
---
<<wait .5>>
<<if visited_count("Ali_Eye") == 0>>
    Ali: Studying a person's features closely. Isn’t that quite personal and intimate? 
    Ali: Except unlike haircuts or tattoos, you’re not changing or altering their look. 
<<elseif visited_count("Ali_Eye") == 1>>
    Ali: How would a non-queer artist approach to drawing someone queer? 
    Ali: It perhaps has something to do with the gaze. 
    Ali: Male gaze, female gaze, queer gaze... when I'm returning you the gaze, how does that make you feel?
    Ali: Am I staring too much?
<<endif>>

<<if $StrokeCount >= $MaxStrokes>>
    <<jump AliSketch>>
<<endif>>
<<pose Ali Sketch .5>>
===
title: Ali_Top
---
<<wait .5>>
<<if visited_count("Ali_Top") == 0>>
    Ali: Black? I guess it is a dresscode of this city. Unfortunately you couldn't escape that even in queer spaces.
//    Ali: Community is so important. 
//    Ali: No matter how different you are with people out there. Knowing there're people sharing similar experiences and stories with you. 
//    Ali: It eases that feeling of isolation.
<<elseif visited_count("Ali_Top") == 1>>
    Ali: On a negative note, I did fall into the trap of altering myself to look more queer. 
    Ali: I thought I'm not queer enough for the longest time.
    Ali: But I tell you, looking queer is a scam! 
    Ali: Pretty sure everyone felt that way. 
<<endif>>

<<if $StrokeCount >= $MaxStrokes>>
    <<jump AliSketch>>
<<endif>>

<<pose Ali Sketch .5>>
===
title: Ali_Tattoo
---
<<wait .5>>
    Ali: I got this tattoo on the last day of a trip.
    <<pose Ali Sketch .5>>
    Ali: A bit spontaneous, but one of my favorite artist was visiting, how could I miss that opportunity?
<<if $StrokeCount >= $MaxStrokes>>
    <<jump AliSketch>>
<<endif>>
===


title: Toni
---
<<camera ToniCam>>
<<if visited_count("Toni") == 0>>
     <<set $NumberOfEncounters +=1>>
    Toni: Hey, I'm Toni. 
    Toni: First time? Ha, I come here all the time.
    <<sketch true>>
    <<jump ToniSketch>>
<<elseif $AliSketched>>
    Toni: I'm going to get some drink.
    <<silence Toni>>
    <<camera Player>>
<<endif>>
===
title: ToniSketch
---
<<if $StrokeCount == 0>>
    Toni: Aha, I was never drawn before.
<<else>> 
    //<<wait .5>>
<<endif>>

<<if $StrokeCount == 1>>
    Toni: Blabla
<<elseif $StrokeCount == 2>>
    Toni: Blablabla
<<elseif $StrokeCount == 3>>
    Toni: Last line before finish sketching.
    <<set $StrokeCount to 0>>
    <<jump ToniSketchFin>>
<<endif>>
<<set $StrokeCount +=1>>
===
title: ToniSketchFin
---
<<sketch false>>
<<camera Player>>
Toni: Thanks! Take this.
<<gift>>
<<set $ToniSketched to true>>
===